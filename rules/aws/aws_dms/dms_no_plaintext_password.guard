#
#####################################
##          AWS Solutions          ##
#####################################
# Rule Identifier:
#   DMS_NO_PLAINTEXT_PASSWORD
#
# Description:
#   DMS Endpoint password must not be a plaintext string or a Ref to a Parameter
#   with a Default value. Can be Ref to a NoEcho Parameter without a Default, or
#   a dynamic reference to a secretsmanager value.
#
# Reports on:
#   AWS::DMS::Endpoint
#
# Evaluates:
#   AWS CloudFormation
#
# Rule Parameters:
#   NA
#
# CFN_NAG Rule Id:
#   F37
#
# Note: this rule works, however it sends the custom message twice for each resource
#
# Scenarios:
# a) SKIP: when there are no AWS::DMS::Endpoint present
# b) PASS: when all AWS::DMS::Endpoint use passwords from secure sources
# c) FAIL: when any AWS::DMS::Endpoint has a Password property not using a secure source
# d) SKIP: when metada has rule suppression for DMS_NO_PLAINTEXT_PASSWORD or CFN_NAG F37

let dms_no_plaintext_password = Resources.*[ Type == 'AWS::DMS::Endpoint'
  Metadata.cfn_nag.rules_to_suppress not exists or
  Metadata.cfn_nag.rules_to_suppress.*.id != "F37"
  Metadata.guard.SuppressedRules not exists or
  Metadata.guard.SuppressedRules.* != "DMS_NO_PLAINTEXT_PASSWORD"
]

# Get any AWS::DMS::Endpoint Refs for Password?
let password_refs = %dms_no_plaintext_password.Properties.Password.'!Ref'

# Rule 1: when dms no plaintext password have Ref to Parameter for Password
rule DMS_USES_SECURE_PARAMETER when
  %dms_no_plaintext_password not empty
{
  Parameters exists
  Parameters not empty
  %password_refs not empty
  let parameter_refs = Parameters.%password_refs
  when %parameter_refs !empty {
    %parameter_refs.Type == 'String'
    %parameter_refs.NoEcho exists
    %parameter_refs.NoEcho == true
    %parameter_refs.Default !exists
  }
}

# Rule 2: when dms no plaintext password and above rule did not pass
rule DMS_USES_SECURE_SERVICE when
  %dms_no_plaintext_password not empty
  not DMS_USES_SECURE_PARAMETER
{
  %dms_no_plaintext_password.Properties.Password !exists
  OR
  %dms_no_plaintext_password.Properties.Password in [ /{{resolve\:secretsmanager\:.*}}/, /{{resolve\:ssm-secure\:.*}}/ ]
  <<
    Violation: DMS Endpoint password must not be a plaintext string or a Ref to a Parameter with a Default value. Can be Ref to a NoEcho Parameter without a Default, or a dynamic reference to a secretsmanager value.
    Fix: Replace plaintext value with a secure one.
  >>
}

# One rule to rule them all...
rule DMS_NO_PLAINTEXT_PASSWORD when
  %dms_no_plaintext_password not empty
{
  DMS_USES_SECURE_PARAMETER
  OR
  DMS_USES_SECURE_SERVICE
}
