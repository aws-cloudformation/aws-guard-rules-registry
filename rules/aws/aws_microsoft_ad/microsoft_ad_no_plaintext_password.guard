#
#####################################
##          AWS Solutions          ##
#####################################
# Rule Identifier:
#   MICROSOFT_AD_NO_PLAINTEXT_PASSWORD
#
# Description:
#  Directory Service Microsoft AD password must not be a plaintext string or a Ref to a Parameter with a Default value.
#  Can be Ref to a NoEcho Parameter without a Default, or a dynamic reference to a secretsmanager/ssm-secure value.
#  with a Default value. Can be Ref to a NoEcho Parameter without a Default, or a dynamic reference to a secretsmanager value.
#
# Reports on:
#   AWS::DirectoryService::MicrosoftAD
#
# Evaluates:
#   AWS CloudFormation
#
# Rule Parameters:
#   NA
#
# CFN_NAG Rule Id:
#   F36
#
# Note: this rule works, however it sends the custom message twice for each resource
#
# Scenarios:
# a) SKIP: when there are no AWS::DirectoryService::MicrosoftAD present
# b) PASS: when all AWS::DirectoryService::MicrosoftAD use passwords from secure sources
# c) FAIL: when any AWS::DirectoryService::MicrosoftAD has a Password property not using a secure source
# d) SKIP: when metadata has rule suppression for MICROSOFT_AD_NO_PLAINTEXT_PASSWORD or CFN_NAG F36

let microsoft_ad_no_plaintext_password = Resources.*[ Type == 'AWS::DirectoryService::MicrosoftAD'
  Metadata.cfn_nag.rules_to_suppress not exists or
  Metadata.cfn_nag.rules_to_suppress.*.id != "F36"
  Metadata.guard.SuppressedRules not exists or
  Metadata.guard.SuppressedRules.* != "MICROSOFT_AD_NO_PLAINTEXT_PASSWORD"
]

# Get any AWS::DirectoryService::MicrosoftAD Refs for Password?
let microsoft_ad_password_refs = %microsoft_ad_no_plaintext_password.Properties.Password.'!Ref'

# Rule 1: when Microsoft AD password no plaintext password have Ref to Parameter for Password
rule MICROSOFT_AD_PASSWORD_USES_SECURE_PARAMETER when
  %microsoft_ad_no_plaintext_password not empty
{
  Parameters exists
  Parameters not empty
  %microsoft_ad_password_refs not empty
  let parameter_refs = Parameters.%microsoft_ad_password_refs
  when %parameter_refs !empty {
    %parameter_refs.Type == 'String'
    %parameter_refs.NoEcho exists
    %parameter_refs.NoEcho == true
    %parameter_refs.Default !exists
  }
}

# Rule 2: when Microsoft AD password no plaintext password and above rule did not pass
rule MICROSOFT_AD_PASSWORD_USES_SECURE_SERVICE when
  %microsoft_ad_no_plaintext_password not empty
  !MICROSOFT_AD_PASSWORD_USES_SECURE_PARAMETER
{
  %microsoft_ad_no_plaintext_password.Properties.Password exists
  %microsoft_ad_no_plaintext_password.Properties.Password in [ /{{resolve\:secretsmanager\:.*}}/, /{{resolve\:ssm-secure\:.*}}/ ]
  <<
    Violation: Microsoft AD Password Endpoint password must not be a plaintext string or a Ref to a Parameter with a Default value. Can be Ref to a NoEcho Parameter without a Default, or a dynamic reference to a secretsmanager value.
    Fix: Replace plaintext value with a secure one.
  >>
}

# One rule to rule them all...
rule MICROSOFT_AD_NO_PLAINTEXT_PASSWORD when
  %microsoft_ad_no_plaintext_password not empty
{
  MICROSOFT_AD_PASSWORD_USES_SECURE_PARAMETER
  OR
  MICROSOFT_AD_PASSWORD_USES_SECURE_SERVICE
}
