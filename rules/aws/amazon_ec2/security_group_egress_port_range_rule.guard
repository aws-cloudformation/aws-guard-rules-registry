#
#####################################
##          AWS Solutions          ##
#####################################
# Rule Identifier:
#    SECURITY_GROUP_EGRESS_PORT_RANGE_RULE
#
# Description:
#   Check if Security Groups found egress with port range instead of just a single PORT
#
# Reports on:
#    [AWS::EC2::SecurityGroupEgress, AWS::EC2::SecurityGroup]
#
# Evaluates:
#    AWS CloudFormation
#
# Rule Parameters:
#    NA
#
# CFN_NAG Rule Id:
#   W29
#
# Scenarios:
# a) SKIP: when there are no Security Egress Groups resource present
# b) PASS: When all Security Egress Groups uses a single port and not range
# c) FAIL: when any Security Egress Groups uses a range of ports
# d) SKIP: when metadata has rule suppression for SECURITY_GROUP_EGRESS_PORT_RANGE_RULE

#
# Select all Security Group Egress resources from incoming template (payload)
#
let security_group_egress_port_range_rule_sg_egress_resources = Resources.*[ Type == 'AWS::EC2::SecurityGroup'
  Metadata.cfn_nag.rules_to_suppress not exists or
  Metadata.cfn_nag.rules_to_suppress.*.id != "W29"
  Metadata.guard.SuppressedRules not exists or
  Metadata.guard.SuppressedRules.* != "SECURITY_GROUP_EGRESS_PORT_RANGE_RULE"
]

let security_group_egress_port_range_rule_sge_resources = Resources.*[ Type == 'AWS::EC2::SecurityGroupEgress'
  Metadata.cfn_nag.rules_to_suppress not exists or
  Metadata.cfn_nag.rules_to_suppress.*.id != "W29"
  Metadata.guard.SuppressedRules not exists or
  Metadata.guard.SuppressedRules.* != "SECURITY_GROUP_EGRESS_PORT_RANGE_RULE"
]

rule SECURITY_GROUP_EGRESS_PORT_RANGE_RULE when %security_group_egress_port_range_rule_sge_resources !empty OR %security_group_egress_port_range_rule_sg_egress_resources !empty {
  let violations_sg = %security_group_egress_port_range_rule_sg_egress_resources[
    Type == 'AWS::EC2::SecurityGroup'
    Properties.SecurityGroupEgress exists
    some Properties.SecurityGroupEgress[*] {
        FromPort exists
        ToPort exists
        FromPort not in ToPort
    }
  ]

  let violations_sge = %security_group_egress_port_range_rule_sge_resources[
    Type == 'AWS::EC2::SecurityGroupEgress'
    Properties.FromPort exists
    Properties.ToPort exists
    Properties.FromPort not in Properties.ToPort
  ]

  %violations_sg empty
  %violations_sge empty
  <<
    Violation: Security Group Egress has a range of ports instead of a single port
    Fix: Use single port instead of range of ports for egress rules
  >>
}
